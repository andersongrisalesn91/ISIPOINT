apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

check.dependsOn 'checkstyle', 'pmd'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "co.com.mirecarga.vendedor"
        minSdkVersion 19
        targetSdkVersion 28
        multiDexEnabled true
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        // URI para appauth que debe ser consistente con la configuración de WSO2 y el clientid
        manifestPlaceholders = [
                'appAuthRedirectScheme': "${appAuthRedirectScheme}"
        ]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }
    ndkVersion '20.1.5948944'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "com.google.android.material:material:1.1.0"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation "androidx.test.ext:junit:1.1.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"
    implementation "com.squareup.okio:okio:${okioVersion}"
    // Retrofit permite acceso configurable al API de servicio
    implementation "com.google.code.gson:gson:${gsonVersion}"
    implementation("com.squareup.retrofit2:converter-gson:${retrofitVersion}") {
        exclude(group: "com.google.code.gson", module: "gson")
    }
    implementation "com.squareup.retrofit2:adapter-rxjava2:${retrofitVersion}"
    implementation "io.reactivex.rxjava2:rxandroid:${rxandroidVersion}"
    implementation "io.reactivex.rxjava2:rxjava:${rxjavaVersion}"
    // Dependencias de net.openid:appauth.  Se debe incluir versión específica de customtabs
    // porque dependendía de la versión anterior 25.3.1 y generaba problemas de compatibilidad
    implementation "androidx.browser:browser:1.2.0"
    implementation("net.openid:appauth:${appauthVersion}") {
        exclude(group: "com.android.support", module: "customtabs")
    }
    // Framework de inyección de dependencias Dagger
    implementation "com.google.dagger:dagger:${daggerVersion}"
    implementation "com.google.dagger:dagger-android:${daggerVersion}"
    implementation "com.google.dagger:dagger-android-support:${daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-android-processor:${daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"
    // Framework de inyección de campos y manejo de eventos
    implementation "com.jakewharton:butterknife:${butterknifeVersion}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterknifeVersion}"
    // Libreria para osmdroid (mapas)
    // implementation "org.osmdroid:osmdroid-wms:${osmdroidVersion}"
    implementation "org.osmdroid:osmdroid-android:${osmdroidVersion}"
    implementation("org.osmdroid:osmdroid-geopackage:${osmdroidVersion}") {
        exclude(group: "mil.nga.geopackage", module: "geopackage-android")
    }
    implementation "com.github.MKergall:osmbonuspack:${osmbonuspackVersion}"
    implementation("mil.nga.geopackage:geopackage-android:${geoPackageVersion}") {
        exclude(group: "com.android.support", module: "support-v13")
    }
    implementation "mil.nga.geopackage:geopackage-core:${geoPackageVersion}"

    // Librería de impresión
    implementation files('../lib/ZSDK_ANDROID_API.jar')

    // Impresora local
    //implementation fileTree(include: ['*.jar', '*.so'], dir: '../jniLibs')
    implementation "com.google.zxing:core:${zxingVersion}"
    implementation files('../lib/CoreWise_Scaner_SDK_V1.0.jar')
    implementation files('../lib/print_SDK.jar')
    // Lector QR code
    implementation "com.google.android.gms:play-services-vision:${playServicesVisionVersion}"

    // MQTT
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.4'
    implementation 'org.eclipse.paho:org.eclipse.paho.android.service:1.1.1'

    //Firease FCM
    implementation 'com.google.firebase:firebase-analytics:17.5.0'
    implementation 'com.google.firebase:firebase-messaging:20.2.4'
    implementation 'com.google.firebase:firebase-messaging-directboot:20.2.4'


    implementation project(':core')
}
